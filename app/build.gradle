apply plugin: 'com.android.application'
apply plugin: "com.mob.sdk"


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.xuke.androidone"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.appVersionCode
        versionName rootProject.ext.appVersionName
        flavorDimensions "versionCode"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters "armeabi", "x86_64", "x86", "armeabi-v7a", "arm64-v8a", "mips", "mips64"
        }

        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "huawei"]
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    productFlavors {
        oppo {}
        vivo {}
        tencent {}
        baidu {}
        adesk {}
        meizu {}
        kuan {}
        wandoujia {}
        huawei {}
        _360 {}
        sumsung {}
        anzhi {}
        xiaomi {}
        sogou {}
        appchina {}
        google {}
        hutui {}
        lenovo {}
    }

    /**
     * 渠道包定制
     */
    productFlavors.all {
        flavor ->
            def channel = name.replaceAll("_", "")
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: channel]
            flavor.versionCode = rootProject.ext.appVersionCode
            flavor.versionName = rootProject.ext.appVersionName
    }

    /**
     * 定义一个装apk文件路径的数组
     * */
    def fileArray = []
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.contains('release')) {
                def variantProd = variant.productFlavors[0]
                def fileName = "obo_${variantProd.versionName}_${variantProd.versionCode}" + "_${variantProd.name.replaceAll("_", "")}.apk"
                outputFileName = fileName
                fileArray.add(outputFile.parentFile.absolutePath + File.separator + fileName)
            }
        }
    }

    //多渠道打包命令：gradle build
    build {
        doLast() {
            forEachFile(fileArray)
        }
    }

    signingConfigs {
        config {
            keyAlias 'obyo'
            keyPassword 'abcthm3b10'
            storeFile file('../key/obo.jks')
            storePassword 'abcthm3b10'
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.config
        }

        release {
            signingConfig signingConfigs.config
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

def forEachFile(fileArray) {
    fileArray.forEach { filePath ->
        rename_and_move_apk(filePath)
    }
}

def rename_and_move_apk(filePath) {
    def prefix = "obo_"
    def version = "${android.defaultConfig.versionName}_${android.defaultConfig.versionCode}"
    def dirName = "${prefix}v$version"
    def outFileDir = rootDir.getAbsolutePath() + File.separator + dirName
    copy {
        from filePath
        into outFileDir
        rename("$prefix${version}_", "")
    }
}

MobSDK {
    appKey "23ebd2c2eb09c"
    appSecret "bc52ffd671306b90ba69073b738b3184"

    SMSSDK {}
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':sdk')
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"

    //bugly
    implementation "com.tencent.bugly:crashreport:$rootProject.buglyVersion"
    implementation "com.tencent.bugly:nativecrashreport:$rootProject.buglyndkVersion"

    //友盟
    implementation "com.umeng.sdk:common:$rootProject.umengCommonVersion"
    implementation "com.umeng.sdk:analytics:$rootProject.umengAnalyticsVersion"

    //权限
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:$rootProject.rxPerssionsVersion"
}
